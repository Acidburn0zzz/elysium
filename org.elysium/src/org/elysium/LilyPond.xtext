grammar org.elysium.LilyPond hidden(WS, SL_COMMENT, ML_COMMENT)

generate lilypond "http://www.elysium.org/LilyPond"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

LilyPond: expressions+=ToplevelExpression*;

ToplevelExpression: Assignment | CommonExpression;

Expression: PropertyAssignment | CommonExpression;

CommonExpression: Command | Block | Scheme | Text | Number;

Assignment: name=(ID | STRING) "=" value=Expression;

PropertyAssignment: id=(ID | STRING) "=" value=Expression;

Block: SimpleBlock | SimultaneousBlock;

SimpleBlock: {SimpleBlock} "{" expressions+=Expression* "}";

SimultaneousBlock: {SimultaneousBlock} "<<" expressions+=Expression* ">>";

Command: SpecialCommand | Reference;

SpecialCommand: Include | Version | Markup | MarkupLines | Context | Paper | Layout | Other;

Include: "\\" "include" importURI=STRING;

Version: "\\" "version" version=STRING;

Markup: "\\" "markup" body=MarkupBody;

MarkupLines: "\\" "markuplines" body=MarkupBody;

MarkupBody: {MarkupBody} command+=(Scheme | UnparsedCommand)* block=UnparsedBlock?;

Context: {Context} "\\" "context" block=UnparsedBlock?;

Paper: "\\" "paper" block=UnparsedBlock;

Layout: "\\" "layout" block=UnparsedBlock;

Other: "\\" keyword=(SpecialCharacter | "(" | ")" | OtherName);

OtherName: "accepts" | "addlyrics" | "alias" | "alternative" | "book" | "bookpart" | "change" | "chordmode" | "chords" | "consists" | "default" | "defaultchild" | "denies" | "description" | "drummode" | "drums" | "figuremode" | "figures" | "grobdescriptions" | "header" | "key" | "lyricmode" | "lyrics" | "lyricsto" | "maininput" | "mark" | "midi" | "name" | "new" | "notemode" | "objectid" | "octave" | "once" | "override" | "partial" | "relative" | "remove" | "repeat" | "rest" | "revert" | "score" | "sequential" | "set" | "simultaneous" | "skip" | "sourcefileline" | "sourcefilename" | "tempo" | "time" | "times" | "transpose" | "type" | "unset" | "with";

SpecialCommandName: "include" | "version" | "markup" | "markuplines" | "context" | "paper" | "layout" | OtherName;

SpecialCharacter: "!" | "+" | "<" | ">" | "[" | "]" | "~";

UnparsedBlock: {UnparsedBlock} "{" expressions+=UnparsedExpression* "}";

UnparsedExpression: UnparsedCommand | UnparsedBlock | Scheme | Text | Number;

UnparsedCommand: "\\" command=(ID | SpecialCommandName);

Reference: "\\" assignment=[Assignment];

Text: value=(SchemeTextValueSegment | "(" | ")" | "'" | "," | ":");

Number: value=INT;

Scheme hidden(WS, SCHEME_SL_COMMENT, SCHEME_ML_COMMENT): "#" value=SchemeExpression;

SchemeExpression: (quoted?="'"? | quasiquoted?="`"? | unquoted?=","?)? value=SchemeValue;

SchemeValue: SchemeBoolean | SchemeList | SchemeBlock | SchemeCharacter | SchemeText | SchemeNumber| SchemeMarkupCommand;

SchemeBoolean: value=SchemeBooleanValue;

SchemeBooleanValue returns ecore::EBoolean: "#" ID;

SchemeList: {SchemeList} (vector?="#")? "(" expressions+=SchemeExpression* ")";

SchemeBlock: "#{" elements+=SchemeBlockElement+ "#}";

SchemeBlockElement: Expression | SchemeReference;

SchemeReference: "$" id=SchemeIdentifier;

SchemeCharacter: "#\\" value=(ID | SpecialCharacter | ANY_OTHER);

SchemeText: value=SchemeTextValue;

SchemeTextValue hidden(): SchemeTextValueSegment ((":" | "::") SchemeTextValueSegment)*;

SchemeTextValueSegment: "\\\\" | "=" | "-" | SpecialCharacter | SpecialCommandName | SchemeIdentifier | STRING | INT | ANY_OTHER;

SchemeIdentifier hidden(): ID ("-" ID)*;

SchemeNumber hidden(): radix=SchemeNumberRadix? value=INT;

SchemeNumberRadix: "#" ID;

SchemeMarkupCommand hidden(): "#:" command=(ID | SpecialCommandName);

terminal STRING: '"' ("\\" ("b" | "t" | "n" | "f" | "r" | '"' | "'" | "\\") | !("\\" | '"'))* '"';

terminal INT returns ecore::EInt: ("0".."9")+;

terminal ID: ("a".."z" | "A".."Z")+;

terminal WS: (" " | "\t" | "\r" | "\n")+;

terminal SL_COMMENT: "%" !("\n" | "\r")* ("\r"? "\n")?;

terminal ML_COMMENT: "%{" -> "%}";

terminal SCHEME_SL_COMMENT: ";" !("\n" | "\r")* ("\r"? "\n")?;

terminal SCHEME_ML_COMMENT: "#!" -> "!#";

terminal ANY_OTHER:	.;