grammar org.elysium.LilyPond hidden(WS, SL_COMMENT, ML_COMMENT)

generate lilypond "http://www.elysium.org/LilyPond"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

LilyPond: expressions+=ToplevelExpression*;

ToplevelExpression: Assignment | CommonExpression;

Expression: PropertyAssignment | CommonExpression;

CommonExpression: Command | Block | Scheme | Text | Number;

Assignment: name=(ID | STRING) "=" value=Expression;

PropertyAssignment: id=SchemeIdentifier "=" value=Expression;

Block: SimpleBlock | SimultaneousBlock;

SimpleBlock: {SimpleBlock} "{" expressions+=Expression* "}";

SimultaneousBlock: {SimultaneousBlock} "<<" expressions+=Expression* ">>";

Command: SpecialCommand | Reference;

SpecialCharacter: "!" | "?" | "+" | "<" | ">" | "[" | "]" | "~";

UnparsedBlock: {UnparsedBlock} "{" expressions+=UnparsedExpression* "}";

UnparsedExpression: PropertyAssignment | UnparsedCommand | UnparsedBlock | Scheme | Text | Number;

UnparsedCommand: "\\" command=(ID | SpecialCommandName);

Reference: "\\" assignment=[Assignment];

Text: value=(SchemeTextValueSegment | "(" | ")" | "'" | "," | ":");

Number: value=INT;

// Commands

SpecialCommand: Include | Version | Markup | MarkupLines | BlockCommand | OutputDefinition | RelativeMusic | ModeChange | MusicWithLyrics | NewContext | ContextDef | Other;

Include: "\\" keyword="include" importURI=STRING;

Version: "\\" keyword="version" version=STRING;

Markup: "\\" keyword="markup" body=MarkupBody;

MarkupLines: "\\" keyword="markuplines" body=MarkupBody;

MarkupBody: {MarkupBody} command+=(Scheme | UnparsedCommand)* block=UnparsedBlock?;

BlockCommand: "\\" keyword=("book" | "bookpart" | "header" | "score") block=SimpleBlock;

OutputDefinition: "\\" keyword=("paper" | "midi" | "layout") block=UnparsedBlock;

RelativeMusic: "\\" keyword="relative" pitch=Pitch? music=Expression;

Pitch: base=ID octaveCheck?="="? octaveShift=Octave? reminderAccidental?="!"? cautionaryAccidental?="?"?;

Octave: up+="'"+ | down+=","+;

ModeChange: "\\" keyword=("chordmode" | "drummode" | "figuremode" | "lyricmode" | "notemode") music=Block;

MusicWithLyrics: "\\" keyword="lyricsto" id=(ID | STRING) lyrics=Expression;

NewContext: "\\" keyword=("new" | "context") context=ID ("=" id=(ID | STRING))? modification=ContextModification? music=Expression;

ContextModification: "\\" "with" block=UnparsedBlock;

ContextDef: "\\" keyword="context" block=UnparsedBlock;

Other: "\\" keyword=(SpecialCharacter | "(" | ")" | OtherName);

OtherName: "accepts" | "addlyrics" | "alias" | "alternative" | "change" | "chords" | "consists" | "default" | "defaultchild" | "denies" | "description" | "drums" | "figures" | "grobdescriptions" | "key" | "lyrics" | "maininput" | "mark" | "name" | "objectid" | "octave" | "once" | "override" | "partial" | "remove" | "repeat" | "rest" | "revert" | "sequential" | "set" | "simultaneous" | "skip" | "sourcefileline" | "sourcefilename" | "tempo" | "time" | "times" | "transpose" | "type" | "unset" | "with";

SpecialCommandName: "include" | "version" | "markup" | "markuplines" | "book" | "bookpart" | "context" | "header" | "score" | "paper" | "midi" | "layout" | "relative" | "chordmode" | "drummode" | "figuremode" | "lyricmode" | "notemode" | "lyricsto" | "new" | "with" | OtherName;

// Scheme

Scheme hidden(WS, SCHEME_SL_COMMENT, SCHEME_ML_COMMENT): ("#" | "$") value=SchemeExpression;

SchemeExpression: reference?="$"? quotations+=("'" | "`" | "," | "@")* value=SchemeValue;

SchemeValue: SchemeBoolean | SchemeList | SchemeBlock | SchemeCharacter | SchemeText | SchemeNumber| SchemeMarkupCommand;

SchemeBoolean: value=SchemeBooleanValue;

SchemeBooleanValue returns ecore::EBoolean: "#" ID;

SchemeList: {SchemeList} (vector?="#")? "(" expressions+=SchemeExpression* ")";

SchemeBlock: "#{" elements+=Expression+ "#}";

SchemeCharacter: "#\\" value=(ID | SpecialCharacter | ANY_OTHER);

SchemeText: value=SchemeTextValue;

SchemeTextValue hidden(): SchemeTextValueSegment (":" (":")? SchemeTextValueSegment)*;

SchemeTextValueSegment: "\\\\" | "=" | "-" | SpecialCharacter | SpecialCommandName | SchemeIdentifier | STRING | INT | ANY_OTHER;

SchemeIdentifier hidden(): ID ("-" ID)*;

SchemeNumber hidden(): radix=SchemeNumberRadix? value=INT;

SchemeNumberRadix: "#" ID;

SchemeMarkupCommand hidden(): "#:" command=(ID | SpecialCommandName);

// Terminals

terminal STRING: '"' ("\\" ("b" | "t" | "n" | "f" | "r" | '"' | "'" | "\\") | !("\\" | '"'))* '"';

terminal INT returns ecore::EInt: ("0".."9")+;

terminal ID: ("a".."z" | "A".."Z")+;

terminal WS: (" " | "\t" | "\r" | "\n")+;

terminal SL_COMMENT: "%" !("\n" | "\r")* ("\r"? "\n")?;

terminal ML_COMMENT: "%{" -> "%}";

terminal SCHEME_SL_COMMENT: ";" !("\n" | "\r")* ("\r"? "\n")?;

terminal SCHEME_ML_COMMENT: "#!" -> "!#";

terminal ANY_OTHER:	.;